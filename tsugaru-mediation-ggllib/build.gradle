buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    }
}

apply plugin: 'com.android.library'

repositories {
    mavenCentral()
}

dependencies {
    compile project(':tsugaru')

    compile 'com.google.code.gson:gson:2.4'
    compile 'com.mcxiaoke.volley:library:1.0.19'
    compile 'com.google.guava:guava:18.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile ('com.squareup.okhttp:mockwebserver:2.4.0') {
        exclude module: 'bcprov-jdk15on'
    }
    testCompile 'com.squareup.assertj:assertj-android:1.0.0'
}

android {
    compileSdkVersion 'android-22'
    buildToolsVersion '22.0.1'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            //runProguard false
            proguardFile getDefaultProguardFile('proguard-android.txt')
        }
    }

    defaultConfig {
        multiDexEnabled = false
    }
}

// For archives
apply plugin: 'com.github.dcendents.android-maven'

// For local install
group 'com.github.k24'
version tsugaruVersion

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
